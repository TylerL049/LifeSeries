plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
    id "me.modmuss50.mod-publish-plugin" version "0.8.4"
}

version = "${project.mod_version}+${stonecutter.current.project}"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

// Include Stonecutter generated sources
sourceSets {
    main {
        java {
            srcDirs += ["build/generated/stonecutter/main/java"]
        }
    }
}

// Java settings
java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

// Loom settings
loom {
    splitEnvironmentSourceSets() // automatically creates clientMain and serverMain

    mods {
        "lifeseries" {
            sourceSet sourceSets.main
            sourceSet sourceSets.clientMain // use clientMain for client-specific sources
        }
    }

    runConfigs.all {
        runDir "../../run" // shared run folder
    }
}

// Repositories
repositories {
    maven { url "https://api.modrinth.com/maven" }
    maven { url 'https://maven.nucleoid.xyz' }
    maven { url "https://maven.tomalbrc.de" }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
    }
    maven { url "https://maven.shedaniel.me/" }
    maven { url 'https://maven.maxhenkel.de/repository/public' }
}

// Fabric API data generation
fabricApi {
    configureDataGeneration()
}

// Dependencies
dependencies {
    minecraft("com.mojang:minecraft:${stonecutter.current.project}")
    mappings("net.fabricmc:yarn:${property("yarn_mappings")}:v2")

    modImplementation("net.fabricmc:fabric-loader:${property("loader_version")}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${property("fabric_version")}")

    if (project.polymer_version != "null") modImplementation include("eu.pb4:polymer-bundled:${project.polymer_version}")
    if (project.blockbench_import_library_version != "null") modImplementation include("de.tomalbrc:blockbench-import-library:${project.blockbench_import_library_version}")

    implementation "de.maxhenkel.voicechat:voicechat-api:${project.voicechat_api_version}"
    if (project.voicechat_mod_version != "null") modRuntimeOnly "maven.modrinth:simple-voice-chat:fabric-${project.voicechat_mod_version}"
    if (project.carpet_mod_version != "null") modRuntimeOnly "maven.modrinth:carpet:${project.carpet_mod_version}"
}

// Process resources
processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.property('mod_version'),
               "targetVersion": project.property('target_version'),
               "minecraftVersion": stonecutter.current.version
    }
}

// Jar task
jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}"}
    }
}

// Mod publishing
publishMods {
    file = remapJar.archiveFile
    changelog = providers.environmentVariable("CHANGELOG")
    type = providers.environmentVariable("VERSION_TYPE")
    displayName = "Life Series ${project.property('mod_version')} for Fabric ${project.property('min_version')}${project.property('min_version') == project.property('max_version') ? '' : '-' + project.property('max_version')}"

    modLoaders.add("fabric")

    def isDevBuild = providers.environmentVariable("DEV_BUILD").getOrElse("true") == "true"

    modrinth {
        projectId = isDevBuild ? "RLDqKhd4" : "aLasQi8P"
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        minecraftVersionRange {
            start = project.property('min_version')
            end = project.property('max_version')
        }
        requires("fabric-api")
        optional("simple-voice-chat")
    }

    if (!isDevBuild) {
        curseforge {
            projectId = "1151647"
            accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
            minecraftVersionRange {
                start = project.property('min_version')
                end = project.property('max_version')
            }
            clientRequired = true
            serverRequired = true
            requires("fabric-api")
            optional("simple-voice-chat")
        }
    }
}

// Maven publishing
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }
    repositories {
        // Add repositories to publish to here if needed
    }
}

// Custom run task
task runClientNamed {
    doFirst {
        runClient.args('--username', 'Player')
    }
    finalizedBy 'runClient'
}